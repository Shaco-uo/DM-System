[EVENTS e_deathmatch_create]
ON=@Logout
	if <src.findid.i_mem_deathmatch>
		ref1=<src.findid.i_mem_deathmatch>
		serv.f_dm_sysmessage <ref1.dtag0.list>,<def.fail>,<src.name> was removed from the match list (logout).
		serv.f_deathmatch_remove_player <src>,<ref1.dtag0.list>
	endif

[EVENTS e_deathmatch_fight]
ON=@Logout
	if <src.findid.i_mem_deathmatch>
		ref1=<src.findid.i_mem_deathmatch>
		serv.f_dm_sysmessage <ref1.dtag0.list>,<def.fail>,<src.name> was removed from the match (logout).
		trigger @Death
	endif
ON=@Death
	if <src.findid.i_mem_deathmatch>
		ref1=<src.findid.i_mem_deathmatch>
		local.team=<ref1.dtag0.team>
		ref2=<serv.list.deathmatch_global.0> // stone
		src.events=-e_deathmatch_fight
		src.f_remove_all_layers
		ref2.tag0.team_<dlocal.team> --
		if <ref2.tag0.team_<dlocal.team>>==0
			serv.f_dm_win <qval <dlocal.team>==1?2:1>,<ref1.dtag0.list>
		endif
		src.f_restore_frombank
		src.restore_me
		src.go <ref1.tag0.old_p>
		if !<isempty <ref1.tag.mount>>
			ref3=<ref1.tag.mount>
			ref3.go=<src.p>
			src.mount <ref3>
			ref1.tag.mount=
		endif
		ref1.tag.old_p=
		return 1
	endif

[EVENTS e_dm_mount_store]
ON=@Dismount
	if <src.findid.i_mem_deathmatch>
		ref1=<src.findid.i_mem_deathmatch>
		ref1.tag.mount=<argo>
		argo.timerf 0,go=<def.dm_mount_zone>
		src.events=-e_dm_mount_store
	endif

[TYPEDEF t_dm_item]
ON=@Dclick
	if !<src.isevent.e_deathmatch_fight>
		destroy 1
		return 1
	endif
ON=@Equiptest
	if !<src.isevent.e_deathmatch_fight>
		destroy 1
		return 1
	endif
ON=@DropOn_Trade
	return 1
ON=@Dropon_Char
	return 1
ON=@Dropon_Item
	if <argo.topobj>!=<src>
		return 1
	endif
ON=@Dropon_Ground
	if <argo.type>==t_spellbook
		return 1
	else
		destroy 1
	endif

[TYPEDEF t_weapon_skillmax]
ON=@Clienttooltip
	ADDCLILOC <def.empty_cliloc>,<def.bfont_lyellow>Max Skill<def.bfont_white>
ON=@Equip
	if <src.<tag0.skill>> < 100.0
		tag.old_skill=<src.<tag0.skill>>
		src.<tag0.skill> = 100.0
	endif
	if !<isempty <tag.skill2>> && <src.<tag0.skill2>> < 100.0
		tag.old_skill2=<src.<tag0.skill2>>
		src.<tag0.skill2> = 100.0
	endif
ON=@UnEquip
	if !<isempty <tag.old_skill>>
		src.<tag0.skill> = <dtag0.old_skill>
		tag.old_skill=
	endif
	if !<isempty <tag.old_skill2>>
		src.<tag0.skill2> = <dtag0.old_skill2>
		tag.old_skill2=
	endif

[TYPEDEF t_armor_increase]
ON=@ClientTooltip
	ADDCLILOC <def.empty_cliloc>,Armor Increase <dtag0.armor_increase>
ON=@Equip
	src.modar += <dtag0.armor_increase>
on=@Unequip
	src.modar -= <dtag0.armor_increase>

[REGIONTYPE r_deathmatch_arena]
ON=@Enter
	if <src.isplayer>
		src.tag.old_karma=<src.karma>
		src.tag.old_kills=<src.kills>
		if !<isempty <tag.music>>
			src.music=<tag.music>
		endif
	endif

ON=@Exit
	if <src.isplayer>
		if !<isempty <src.tag.old_karma>>
			src.karma=<src.dtag0.old_karma>
			src.tag.old_karma=
		endif
		if !<isempty <src.tag.old_kills>>
			src.kills=<src.dtag0.old_kills>
			src.tag.old_kills=
		endif
		src.clear_mems_flags
	endif

[EOF]