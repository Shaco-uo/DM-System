[EVENTS e_deathmatch_create]
ON=@Logout
	if <src.findid.i_mem_deathmatch>
		ref2=<src.findid.i_mem_deathmatch>
		ref1=<ref2.tag0.stone>
		serv.f_dm_sysmessage <ref1>,<ref2.dtag0.list>,<def.fail>,<src.name> was removed from the match list (logout).
		serv.f_deathmatch_remove_player <ref1>,<src>,<ref2.dtag0.list>
	endif

[EVENTS e_deathmatch_fight]
ON=@Logout
	if <src.findid.i_mem_deathmatch>
		ref1=<src.findid.i_mem_deathmatch>
		serv.f_dm_sysmessage <ref1.tag0.stone>,<ref1.dtag0.list>,<def.fail>,<src.name> was removed from the match (logout).
		trigger @Death
	endif

ON=@Death
	if <src.findid.i_mem_deathmatch>
		ref2=<src.findid.i_mem_deathmatch>
		ref1=<ref2.tag0.stone> // stone
		if <ref2.dtag0.team>==1
			serv.list.dm_stone_<ref1>.17=<eval <serv.list.dm_stone_<ref1>.17>-1> // team 1 count
			local.c=<serv.list.dm_stone_<ref1>.17>
		else
			serv.list.dm_stone_<ref1>.18=<eval <serv.list.dm_stone_<ref1>.18>-1> // team 2 count
			local.c=<serv.list.dm_stone_<ref1>.18>
		endif
		if <dlocal.c>==0
			serv.f_dm_win <ref1>,<qval <ref2.dtag0.team>==1?2:1>,<ref2.dtag0.list>
		else
			src.events=-e_deathmatch_fight
			if <ref2.dtag0.dm_maxskill>==1 // max skills
				serv.f_dm_restore_skills <src>,<uid>
			endif
			if <ref2.dtag0.dm_equip>==1
				src.f_remove_all_layers
				src.f_restore_frombank
			endif
			if <eval <serv.list.dm_stone_<ref1>.19>>==1
				src.clear_mems_flags
			endif
			src.restore_me
			src.go <ref2.tag0.old_p>
			if !<isempty <ref2.tag.mount>>
				ref3=<ref2.tag.mount>
				ref3.go=<src.p>
				src.mount <ref3>
				ref2.tag.mount=
			endif
			ref2.tag.old_p=
		endif
		return 1
	endif

[EVENTS e_dm_mount_store]
ON=@Dismount
	if <src.findid.i_mem_deathmatch>
		ref1=<src.findid.i_mem_deathmatch>
		ref1.tag.mount=<argo>
		argo.timerf 0,go=<def.dm_mount_zone>
		src.events=-e_dm_mount_store
	endif

[TYPEDEF t_dm_item]
ON=@Dclick
	if !<src.isevent.e_deathmatch_fight>
		destroy 1
		return 1
	endif
ON=@Equiptest
	if !<src.isevent.e_deathmatch_fight>
		destroy 1
		return 1
	endif
ON=@DropOn_Trade
	return 1
ON=@Dropon_Char
	return 1
ON=@Dropon_Item
	if <argo.topobj>!=<src>
		return 1
	endif
ON=@Dropon_Ground
	if <argo.type>==t_spellbook
		return 1
	else
		destroy 1
	endif

// [TYPEDEF t_armor_increase]
// ON=@ClientTooltip
	// ADDCLILOC <def.empty_cliloc>,Armor Increase <dtag0.armor_increase>
// ON=@Equip
// 	src.modar += <dtag0.armor_increase>
// on=@Unequip
// 	src.modar -= <dtag0.armor_increase>

[EOF]