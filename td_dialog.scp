[DIALOG d_deathmatch_main]
300,200
PAGE 0
if <src.isevent.e_deathmatch_fight>
	src.sysmessage @,,1 You cannot use this function while in a match.
	return 1
endif
f_dlg_teamdm_sidebar
f_dlg_deathmatch_<src.dctag0.deathmatch_page>

[DIALOG d_deathmatch_main PREBUTTON]
src.dialogclose d_deathmatch_main

[DIALOG d_deathmatch_main BUTTON]
ON=0
	if <src.dctag0.deathmatch_page>==2
		src.ctag.deathmatch_opt=
		src.ctag.deathmatch_page=1
		sdialog d_deathmatch_main
	else
		src.ctag.deathmatch_page=
	endif

ON=1 10 // select list
	if <src.dctag0.deathmatch_page>==2
		src.ctag.deathmatch_page=1
		src.ctag.deathmatch_opt=
	else
		src.ctag.deathmatch_page=2
		src.ctag.deathmatch_opt=<argn1>
	endif
	sdialog d_deathmatch_main

ON=101 // create list
	src.ctag.match_amt=
	src.ctag.deathmatch_page=
	src.ctag.match_private=
	sdialog d_deathmatch_main

ON=102 // avalaible match list
	src.ctag.deathmatch_page=1
	sdialog d_deathmatch_main

ON=103 // player amt+
	src.ctag0.match_amt ++
	if <src.dctag0.match_amt> > 5
		src.ctag.match_amt=1
	endif
	sdialog d_deathmatch_main

ON=104 // private/public
	if <isempty <src.ctag.match_private>>
		src.ctag.match_private=<R1000,9999>
		src.sysmessage @<def0.dm_smsg_color>,,1 New match password set to <src.dctag0.match_private>.
	else
		if !<isnum <argtxt[1]>>
			src.sysmessage @<def.fail>,,1 Invalid Number.
		elif <dargtxt[1]>!=<src.dctag0.match_private>
			src.ctag.match_private=<argtxt[1]>
			src.sysmessage @<def0.dm_smsg_color>,,1 New match password set to <src.dctag0.match_private>.
		else
			src.ctag.match_private=
		endif
	endif
	sdialog d_deathmatch_main

ON=105 // create list
	ref1=<serv.list.deathmatch_global.0>
	local.count=<eval <serv.list.deathmatch_global.count>-1>
	if <src.findid.i_mem_deathmatch.isvalid>
		src.sysmessage @<def.fail>,,1 You already have a deathmatch list
		return 1
	endif
	serv.list.deathmatch_<src>.clear
	serv.list.deathmatch_<src>.add = <src>
	serv.list.deathmatch_global.add = <src>,<src.dctag0.match_amt>,<src.dctag0.match_private>
	src.newitem=i_mem_deathmatch
	new.tag.list=<eval <dlocal.count>+1>
	new.timer=<def0.dm_list_duration>
	new.equip <src>
	src.sysmessage @<def0.dm_smsg_color>,,1 You have created a <src.dctag0.match_amt> vs <src.dctag0.match_amt> deathmatch list.
	src.ctag.deathmatch_page=1
	src.ctag.match_amt=
	src.ctag.match_private=
	sdialog d_deathmatch_main

ON=106 // delete list
	local.list=<src.findid.i_mem_deathmatch.dtag0.list>
	serv.f_deathmatch_remove_list <dlocal.list>
	serv.f_dm_check_queue <dlocal.list>
	src.sysmessage @,,1 You deathmatch list has been cleared.
	src.ctag.deathmatch_page=1
	src.timerf 0,dialog d_deathmatch_main

ON=107 // join list
	ref1=<serv.list.deathmatch_global.0>
	args=<serv.list.deathmatch_global.<src.dctag0.deathmatch_opt>>
	ref2=<argv[0]> // player 1
	local.amt=<argv[1]>
	local.max_amt=<argv[1]>*2
	serv.list.deathmatch_<ref2>.add = <src>
	src.newitem=i_mem_deathmatch
	new.tag.list=<src.dctag0.deathmatch_opt>
	new.timer=-1
	new.equip <src>
	src.sysmessage @<def0.dm_smsg_color>,,1 You have joined a <dlocal.amt> vs <dlocal.amt> deathmatch list.
	if <serv.list.deathmatch_<ref2>.count>==<dlocal.max_amt>
		serv.f_dm_message <src.dctag0.deathmatch_opt>,<def0.dm_msg_color>,Deathmatch Ready
	endif
	sdialog d_deathmatch_main

ON=108 // leave list
	ref1=<src.findid.i_mem_deathmatch>
	serv.f_deathmatch_remove_player <src>,<ref1.dtag0.list>
	src.sysmessage @<def.fail>,,1 You leave the match list.
	sdialog d_deathmatch_main

ON=109 // start match
	ref1=<serv.list.deathmatch_global.0>
	if <ref1.dtag0.mode>==0
		serv.f_deathmatch_prestart <src.dctag0.deathmatch_opt>
		src.ctag.deathmatch_opt=
	endif
ON=110 // add to queue
	ref1=<serv.list.deathmatch_global.0>
	if <ref1.dtag0.mode> >= 1
		ref2=<strarg <serv.list.deathmatch_global.<src.dctag0.deathmatch_opt>>>
		ref3=<ref2.findid.i_mem_deathmatch>
		ref3.timer=-1
		serv.list.deathmatch_queue.add <ref3>
		serv.f_dm_sysmessage <dtag0.list>,<def0.dm_smsg_color>,Your deathmatch list has been placed on queue (<serv.list.deathmatch_queue.count>).
	endif

ON=111 // join private list
	ref1=<serv.list.deathmatch_global.0>
	args=<serv.list.deathmatch_global.<src.dctag0.deathmatch_opt>>
	ref2=<argv[0]> // player 1
	local.amt=<argv[1]>
	local.max_amt=<argv[1]>*2
	local.private=<argv[2]>
	if <isempty <argtxt[2]>> || !<isnum <argtxt[2]>> || <dargtxt[2]>!=<dlocal.private>
		src.sysmessage @<def.fail>,,1 Invalid Code.
	else
		serv.list.deathmatch_<ref2>.add = <src>
		src.newitem=i_mem_deathmatch
		new.tag.list=<src.dctag0.deathmatch_opt>
		new.timer=-1
		new.equip <src>
		src.sysmessage @<def0.dm_smsg_color>,,1 You have joined a <dlocal.amt> vs <dlocal.amt> deathmatch list.
		if <serv.list.deathmatch_<ref2>.count>==<dlocal.max_amt>
			serv.f_dm_message <src.dctag0.deathmatch_opt>,<def0.dm_msg_color>,Deathmatch Ready
		endif
	endif
	sdialog d_deathmatch_main

ON=203 220 // reorder teams
	local.pos=<eval <argn1>-201>
	ref2=<strarg <serv.list.deathmatch_global.<src.dctag0.deathmatch_opt>>>
	ref3=<serv.list.deathmatch_<ref2>.<dlocal.pos>> // selected player
	ref4=<serv.list.deathmatch_<ref2>.<eval <dlocal.pos>-1>>
	serv.list.deathmatch_<ref2>.<dlocal.pos> = <ref4>
	serv.list.deathmatch_<ref2>.<eval <dlocal.pos>-1> = <ref3>
	sdialog d_deathmatch_main

[EOF]